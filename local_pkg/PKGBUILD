# Maintainer: Antony-hash512
pkgname=zero-kelvin-testing
pkgver=$(grep '^version =' ../Cargo.toml | cut -d '"' -f 2)
pkgrel=1
pkgdesc="Cold storage backup utility (Rust port)"
arch=('x86_64')
url="https://github.com/Antony-hash512/Zero-Kelvin-Stazis"
license=('GPL3')
depends=('glibc' 'gcc-libs' 'squashfs-tools' 'squashfs-tools-ng' 'cryptsetup' 'squashfuse' 'rclone' 'util-linux')
makedepends=('cargo' 'git' 'fish' 'bats' 'just') # Добавил тестовые зависимости
provides=('zero-kelvin' '0k' '0k-core' '0k-safe-rm')
conflicts=('zero-kelvin')

# Абсолютный путь к корню проекта (вычисляется при парсинге PKGBUILD)
# Нужен, чтобы избежать конфликта имён: папка src проекта vs папка src от makepkg
_projectdir="$(cd "$(dirname "${BASH_SOURCE[0]}")/.." && pwd)"

source=()
md5sums=()

prepare() {
    # Очищаем целевую папку
    rm -rf "$srcdir/$pkgname"
    mkdir -p "$srcdir/$pkgname"
 
    # Копируем нужные каталоги и файлы
    cp -r "$_projectdir/src" "$srcdir/$pkgname/"
    cp -r "$_projectdir/tests" "$srcdir/$pkgname/"

    cp "$_projectdir/Cargo.toml" "$srcdir/$pkgname/"
    cp "$_projectdir/Cargo.lock" "$srcdir/$pkgname/"
    cp "$_projectdir/Justfile" "$srcdir/$pkgname/"
    cp "$_projectdir/README.md" "$srcdir/$pkgname/"
    cp "$_projectdir/LICENSE.GPLv3" "$srcdir/$pkgname/"
    
    echo "DEBUG: Copied source structure:"
    ls -F "$srcdir/$pkgname/"
}

options=(!debug !lto)


build() {
    cd "$srcdir/$pkgname"
    export RUSTUP_TOOLCHAIN=stable
    # Явно указываем папку для артефактов внутри временного каталога
    export CARGO_TARGET_DIR="$srcdir/$pkgname/target"
    cargo build --release --locked
}

check() {
    cd "$srcdir/$pkgname"
    export CARGO_TARGET_DIR="$srcdir/$pkgname/target"
    cargo test --release --locked
    
    # Запуск интеграционных тестов на бинарниках, которые мы только что собрали
    # Используем --no-build-release, так как release-бинарники уже собраны в этапе build()
    fish tests/run_shell_tests.fish --no-build-release --no-root
}

package() {
    cd "$srcdir/$pkgname"
    install -Dm755 target/release/0k-core "$pkgdir/usr/bin/0k-core"
    install -Dm755 target/release/0k "$pkgdir/usr/bin/0k"
    install -Dm755 target/release/0k-safe-rm "$pkgdir/usr/bin/0k-safe-rm"
    
    # Create alias symlink
    ln -s 0k "$pkgdir/usr/bin/zero-kelvin"

    # Установка лицензии и документации
    install -Dm644 LICENSE.GPLv3 "$pkgdir/usr/share/licenses/$pkgname/LICENSE.GPLv3"
    install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
